// SPDX-License-Identifier: BUSL-1.1
pragma solidity =0.7.6;

import './interfaces/IUniswapV3Pool.sol';

import './NoDelegateCall.sol';

import './libraries/LowGasSafeMath.sol';
import './libraries/SafeCast.sol';
import './libraries/Tick.sol';
import './libraries/TickBitmap.sol';
import './libraries/Position.sol';
import './libraries/Oracle.sol';

import './libraries/FullMath.sol';
import './libraries/FixedPoint128.sol';
import './libraries/TransferHelper.sol';
import './libraries/TickMath.sol';
import './libraries/LiquidityMath.sol';
import './libraries/SqrtPriceMath.sol';
import './libraries/SwapMath.sol';

import './interfaces/IUniswapV3PoolDeployer.sol';
import './interfaces/IUniswapV3Factory.sol';
import './interfaces/IERC20Minimal.sol';
import './interfaces/callback/IUniswapV3MintCallback.sol';
import './interfaces/callback/IUniswapV3SwapCallback.sol';
import './interfaces/callback/IUniswapV3FlashCallback.sol';

contract UniswapV3Pool is IUniswapV3Pool, NoDelegateCall {
    using LowGasSafeMath for uint256;
    using LowGasSafeMath for int256;
    using SafeCast for uint256;
    using SafeCast for int256;
    using Tick for mapping(int24 => Tick.Info);
    using TickBitmap for mapping(int16 => uint256);
    using Position for mapping(bytes32 => Position.Info);
    using Position for Position.Info;
    using Oracle for Oracle.Observation[65535];

    /// @inheritdoc IUniswapV3PoolImmutables
    /// å·¥å‚åœ°å€ï¼Œç”¨äº onlyFactoryOwner ï¼Œåªå…è®¸ factory owner è®¾ç½®å’Œæ”¶é›†åè®®è´¹
    address public immutable override factory;
    /// @inheritdoc IUniswapV3PoolImmutables
    /// æ± å­çš„ token0 åœ°å€
    address public immutable override token0;
    /// @inheritdoc IUniswapV3PoolImmutables
    /// æ± å­çš„ token1 åœ°å€
    address public immutable override token1;
    /// @inheritdoc IUniswapV3PoolImmutables
    /// æ± å­çš„ fee (100, 500, 3000, 10000)ï¼Œæ¯å•ä½æ˜¯ç™¾ä¸‡åˆ†ä¹‹ä¸€
    uint24 public immutable override fee;

    /// @inheritdoc IUniswapV3PoolImmutables
    /// fee å¯¹åº”çš„ tick é—´è·ï¼Œ{100: 1, 500: 10, 3000: 60, 10000: 200}ï¼Œé—´è·è¶Šå¤§ï¼Œæ»‘ç‚¹è¶Šå¤§
    /// ä¼ å…¥åˆçº¦çš„ tick éƒ½å¿…é¡»æ˜¯å¯ä»¥æ•´é™¤ tickSpacing çš„
    int24 public immutable override tickSpacing;

    /// @inheritdoc IUniswapV3PoolImmutables
    /// æ¯ä¸ªtickå¯ä»¥æ‰¿è½½çš„æœ€å¤§æµåŠ¨æ€§ï¼Œç­‰äº type(uint128).max / numTicksï¼Œé¿å…æ€»å’Œæº¢å‡º uint128
    uint128 public immutable override maxLiquidityPerTick;

    struct Slot0 {
        // the current price
        // å½“å‰ä¸¤ä¸ª token ä»·æ ¼çš„å¹³æ–¹æ ¹ï¼Œç”±64ä½æ•´æ•°å’Œ96å°æ•°ç»„æˆ
        // sqrt(P) = sqrt(y/x)
        // åˆçº¦ä½¿ç”¨ sqrt(P) è€Œä¸æ˜¯ P æœ‰ä¸¤ä¸ªåŸå› ï¼š
        // 1. è®¡ç®—æ¯”è¾ƒæ–¹ä¾¿ï¼Œå› ä¸ºä¸€ä¸ªæ—¶åˆ»åªæœ‰å…¶ä¸­ä¸€ä¸ªå€¼ä¼šå˜åŒ–ã€‚å½“åœ¨ä¸€ä¸ªtickå†…äº¤æ˜“æ—¶ï¼Œåªæœ‰ä»·æ ¼ï¼ˆå³sqrt(ğ‘ƒ)ï¼‰å‘ç”Ÿå˜åŒ–ï¼›
        // å½“ç©¿è¶Šä¸€ä¸ªtickæˆ–è€…é“¸é€ /é”€æ¯æµåŠ¨æ€§æ—¶ï¼Œåªæœ‰æµåŠ¨æ€§ï¼ˆå³ğ¿ï¼‰å‘ç”Ÿå˜åŒ–ã€‚è¿™é¿å…äº†åœ¨è®°å½•è™šæ‹Ÿä½™é¢æ—¶å¯èƒ½é‡åˆ°çš„èˆå…¥è¯¯å·®é—®é¢˜ã€‚
        // 2. â€‹sqrt(P) ä¸ L ä¹‹é—´æœ‰ä¸€ä¸ªå…³ç³»ï¼šL = delta Y / delta sqrt(P)ï¼Œä¸ºäº†ä½¿ç”¨è¿™ä¸ªå…¬å¼ï¼Œé¿å…å½“è®¡ç®—äº¤æ˜“æ—¶è¿›è¡Œä»»ä½•å¼€æ ¹å·è¿ç®—ã€‚
        // è¿™ä¸ªå…¬å¼å¯ä»¥ç†è§£ä¸ºï¼ŒæµåŠ¨æ€§ä¹Ÿå¯ä»¥è¢«çœ‹ä½œtoken1çš„ï¼ˆæ— è®ºæ˜¯çœŸå®è¿˜æ˜¯è™šæ‹Ÿçš„ï¼‰æ•°é‡å˜åŒ–ä¸ä»·æ ¼sqrt(ğ‘ƒ)å˜åŒ–çš„æ¯”ä¾‹
        // æ¨å¯¼ï¼š
        // å‰æï¼šL^2 = xyï¼ŒP = y / x
        // y = L * L / x
        // y = L * L / (y / P)
        // y = L * L * (P / y)
        // y * y = L * L * P
        // y = L * sqrt(P)

        // å‡è®¾ ğ‘¡0 å’Œ ğ‘¡1 æ—¶åˆ»ï¼Œå¯¹åº”çš„ ğ‘¦0 å’Œ ğ‘¦1 åˆ†åˆ«ä¸ºï¼š
        // y0 = L * sqrt(P0)
        // y1 = L * sqrt(P1)
        // å› æ­¤ï¼Œy1 - y0 = L * (sqrt(P1) - sqrt(P0))
        // L = (y1 - y0) / (sqrt(P1) - sqrt(P0)) = delta Y / delta sqrt(P)
        uint160 sqrtPriceX96;
        // the current tick
        // ä½äºå½“å‰ä»·æ ¼çš„æœ€æ¥è¿‘çš„tick
        // tick i çš„ sqrt(P) = 1.0001 ** (i / 2)
        // sqrt(P) å¯¹åº”çš„ tick i ç­‰äºä»¥sqrt(1.0001)ä¸ºåº•ï¼Œsqrt(P)çš„å¯¹æ•°ã€‚i = log(sqrt(1.0001))sqrt(P)
        // å­˜ç–‘ï¼šè¿™é‡Œå­˜å‚¨çš„tickæ˜¯å¦é™¤ä»¥äº†tickSpacingï¼ŒçŒœæµ‹åº”è¯¥ä¸ä¼šï¼Œå¦åˆ™å¯¹åº”ä¸ä¸Šå½“å‰ä»·æ ¼
        int24 tick;
        // the most-recently updated index of the observations array
        // è§‚æµ‹æ•°ç»„æœ€æ–°çš„è§‚æµ‹ç´¢å¼•ï¼Œå³æœ€åä¸€æ¬¡å†™å…¥çš„è§‚æµ‹ç‚¹ç´¢å¼•
        uint16 observationIndex;
        // the current maximum number of observations that are being stored
        // é¢„è¨€æœºæ•°ç»„å½“å‰é•¿åº¦
        // è¿™ä¸ªå€¼Oracle.writeä¸­æ›´æ–°ï¼Œå¯ä»¥æ¯æ¬¡å¢åŠ 1ï¼Œä¹Ÿå¯ä»¥çªå˜å¢å¤§
        uint16 observationCardinality;
        // the next maximum number of observations to store, triggered in observations.write
        // è§‚æµ‹æ•°ç»„èƒ½å¤Ÿæ‰©å±•åˆ°çš„ä¸‹ä¸€ä¸ªåŸºæ•°çš„å¤§å°ï¼Œé€šè¿‡ increaseObservationCardinalityNext å¢åŠ 
        // è§‚æµ‹æ•°æ®å­˜å‚¨åœ¨ä¸€ä¸ªå®šé•¿çš„æ•°ç»„é‡Œï¼Œå½“ä¸€ä¸ªæ–°çš„è§‚æµ‹è¢«å­˜å‚¨å¹¶ä¸” observationCardinalityNext è¶…è¿‡ observationCardinality çš„æ—¶å€™å°±ä¼šæ‰©å±•ã€‚
        // å¦‚æœä¸€ä¸ªæ•°ç»„ä¸èƒ½è¢«æ‰©å±•ï¼ˆä¸‹ä¸€ä¸ªåŸºæ•°ä¸ç°åœ¨çš„åŸºæ•°ç›¸åŒï¼‰ï¼Œæ—§çš„è§‚æµ‹å°±ä¼šè¢«è¦†ç›–
        // æ•°ç»„æ‰©å±•éœ€è¦å¤–éƒ¨è°ƒç”¨ increaseObservationCardinalityNext å‡½æ•°ï¼Œè¿™æ˜¯uniswapåˆ†æ•£gasæ¶ˆè€—çš„æ–¹å¼
        uint16 observationCardinalityNext;
        // the current protocol fee as a percentage of the swap fee taken on withdrawal
        // represented as an integer denominator (1/x)%
        // åè®®æ‰‹ç»­è´¹ï¼Œè¡¨ç¤ºäº¤æ˜“è€…æ”¯ä»˜æ‰‹ç»­è´¹çš„éƒ¨åˆ†æ¯”ä¾‹å°†åˆ†ç»™åè®®ï¼Œ0, 1/4, 1/5, 1/6, 1/7, 1/8, 1/9 æˆ–è€… 1/10
        // ä½¿ç”¨8ä½å­˜å‚¨ï¼Œé«˜å››ä½æ˜¯token1çš„æ‰‹ç»­è´¹ï¼Œä½å››ä½æ˜¯token0çš„æ‰‹ç»­è´¹
        uint8 feeProtocol;
        // whether the pool is locked
        bool unlocked;
    }
    /// @inheritdoc IUniswapV3PoolState
    /// æ± å­çš„ç¬¬ä¸€ä¸ªæ’æ§½å­˜å‚¨çš„å…¨å±€å˜é‡ï¼ŒåŒ…æ‹¬sqrtPrice, tick, oracle variables, feeProtocal, unlocked
    Slot0 public override slot0;

    /// @inheritdoc IUniswapV3PoolState
    /// è¡¨ç¤ºè¯¥åˆçº¦åˆ°ç°åœ¨ä¸ºæ­¢ï¼Œæ¯ä¸€ä»½è™šæ‹ŸæµåŠ¨æ€§ï¼ˆğ¿ï¼‰è·å–çš„ token0 çš„æ‰‹ç»­è´¹ï¼Œä½¿ç”¨æ— ç¬¦å·å®šç‚¹æ•°ï¼ˆ128x128æ ¼å¼ï¼‰è¡¨ç¤º
    uint256 public override feeGrowthGlobal0X128;
    /// @inheritdoc IUniswapV3PoolState
    /// è¡¨ç¤ºè¯¥åˆçº¦åˆ°ç°åœ¨ä¸ºæ­¢ï¼Œæ¯ä¸€ä»½è™šæ‹ŸæµåŠ¨æ€§ï¼ˆğ¿ï¼‰è·å–çš„ token1 çš„æ‰‹ç»­è´¹ï¼Œä½¿ç”¨æ— ç¬¦å·å®šç‚¹æ•°ï¼ˆ128x128æ ¼å¼ï¼‰è¡¨ç¤º
    uint256 public override feeGrowthGlobal1X128;

    // accumulated protocol fees in token0/token1 units
    struct ProtocolFees {
        uint128 token0;
        uint128 token1;
    }
    /// @inheritdoc IUniswapV3PoolState
    /// ä»¥æ¯ç§ä»£å¸è¡¨ç¤ºçš„ç´¯è®¡æœªè¢«é¢†å–çš„åè®®æ‰‹ç»­è´¹ï¼Œä»¥æ— ç¬¦å·uint128ç±»å‹è¡¨ç¤ºã€‚
    /// é€šè¿‡è°ƒç”¨ collectProtocol æ–¹æ³•é¢†å–
    ProtocolFees public override protocolFees;

    /// @inheritdoc IUniswapV3PoolState
    /// èµ„é‡‘æ± å½“å‰åœ¨èŒƒå›´å†…çš„å¯ç”¨æµåŠ¨æ€§
    /// L = sqrt(xy)
    uint128 public override liquidity;

    /// @inheritdoc IUniswapV3PoolState
    /// ä¸€ä¸ª tick å¯¹åº”çš„ä¿¡æ¯ï¼ˆæ³¨é‡ŠæŸ¥çœ‹Tickæ–‡ä»¶ï¼‰ï¼Œtick èŒƒå›´ [-887272, 887272]
    mapping(int24 => Tick.Info) public override ticks;
    /// @inheritdoc IUniswapV3PoolState
    /// ä¸ºäº†æ›´é«˜æ•ˆå¯»æ‰¾ä¸‹ä¸€ä¸ªå·²åˆå§‹åŒ–çš„tickï¼Œåˆçº¦ä½¿ç”¨ä¸€ä¸ªä½å›¾tickBitmapè®°å½•å·²åˆå§‹åŒ–çš„tickã€‚
    /// å¦‚æœtickå·²è¢«åˆå§‹åŒ–ï¼Œä½å›¾ä¸­å¯¹åº”äºè¯¥tickåºå·çš„ä½ç½®è®¾ç½®ä¸º1ï¼Œå¦åˆ™ä¸º0ã€‚
    /// int24 compressed = tick / tickSpacing;
    /// wordPos = int16(tick >> 8); tickåœ¨ç¬¬å‡ ä¸ªå­—
    /// bitPos = uint8(tick % 256); tickåœ¨è¿™ä¸ªå­—çš„ç¬¬å‡ ä½
    mapping(int16 => uint256) public override tickBitmap;
    /// @inheritdoc IUniswapV3PoolState
    /// ä»“ä½ä¿¡æ¯ï¼ˆæ³¨é‡ŠæŸ¥çœ‹Positionæ–‡ä»¶ï¼‰ã€‚key ä¸º keccak256(abi.encodePacked(owner, tickLower, tickUpper))
    mapping(bytes32 => Position.Info) public override positions;
    /// @inheritdoc IUniswapV3PoolState
    /// é¢„è¨€æœºæ•°æ®è§‚æµ‹æ•°ç»„ï¼Œæœ€å¤š 65535 ä½ï¼Œè¶…è¿‡æ—¶ä¼šé‡æ–°å¼€å§‹ï¼Œæ—§æ•°æ®è¢«è¦†ç›–
    Oracle.Observation[65535] public override observations;

    /// @dev Mutually exclusive reentrancy protection into the pool to/from a method. This method also prevents entrance
    /// to a function before the pool is initialized. The reentrancy guard is required throughout the contract because
    /// we use balance checks to determine the payment status of interactions such as mint, swap and flash.
    /// é¿å…é‡å…¥çš„ä¿®é¥°å™¨
    modifier lock() {
        require(slot0.unlocked, 'LOK');
        slot0.unlocked = false;
        _;
        slot0.unlocked = true;
    }

    /// @dev Prevents calling a function from anyone except the address returned by IUniswapV3Factory#owner()
    /// åªå…è®¸factory owner è®¾ç½® protocol fee å’Œæ”¶é›† protocol fee
    modifier onlyFactoryOwner() {
        require(msg.sender == IUniswapV3Factory(factory).owner());
        _;
    }

    constructor() {
        int24 _tickSpacing;
        // factory åˆçº¦åˆ›å»º pool åˆçº¦æ—¶ï¼Œæš‚æ—¶å­˜å‚¨äº†è¿™äº›å¿…è¦å‚æ•°ï¼Œç”¨å®Œå³åˆ ï¼Œè¯¦è§ factory.createPool - UniswapV3PoolDeployer.deploy
        (factory, token0, token1, fee, _tickSpacing) = IUniswapV3PoolDeployer(msg.sender).parameters();
        tickSpacing = _tickSpacing;

        // æ¯ä¸ªtickå¯ä»¥æ‰¿è½½çš„æœ€å¤§æµåŠ¨æ€§ï¼Œç­‰äº type(uint128).max / numTicksï¼Œé¿å…æ€»å’Œæº¢å‡º uint128
        maxLiquidityPerTick = Tick.tickSpacingToMaxLiquidityPerTick(_tickSpacing);
    }

    /// @dev Common checks for valid tick inputs.
    /// å¯¹tickçš„åŸºæœ¬æ ¡éªŒ
    function checkTicks(int24 tickLower, int24 tickUpper) private pure {
        require(tickLower < tickUpper, 'TLU');
        require(tickLower >= TickMath.MIN_TICK, 'TLM');
        require(tickUpper <= TickMath.MAX_TICK, 'TUM');
    }

    /// @dev Returns the block timestamp truncated to 32 bits, i.e. mod 2**32. This method is overridden in tests.
    /// åŒºå—æ—¶é—´ï¼Œuint32è¡¨ç¤º
    function _blockTimestamp() internal view virtual returns (uint32) {
        return uint32(block.timestamp); // truncation is desired
    }

    /// @dev Get the pool's balance of token0
    /// @dev This function is gas optimized to avoid a redundant extcodesize check in addition to the returndatasize
    /// check
    /// è¿”å›æ± å­token0çš„ä½™é¢
    function balance0() private view returns (uint256) {
        (bool success, bytes memory data) = token0.staticcall(
            abi.encodeWithSelector(IERC20Minimal.balanceOf.selector, address(this))
        );
        require(success && data.length >= 32);
        return abi.decode(data, (uint256));
    }

    /// @dev Get the pool's balance of token1
    /// @dev This function is gas optimized to avoid a redundant extcodesize check in addition to the returndatasize
    /// check
    /// è¿”å›æ± å­token0çš„ä½™é¢
    function balance1() private view returns (uint256) {
        (bool success, bytes memory data) = token1.staticcall(
            abi.encodeWithSelector(IERC20Minimal.balanceOf.selector, address(this))
        );
        require(success && data.length >= 32);
        return abi.decode(data, (uint256));
    }

    /// @inheritdoc IUniswapV3PoolDerivedState
    /// @notice è¿”å›ä¸€ä¸ªtickèŒƒå›´å†…çš„æ•°æ®å¿«ç…§ï¼ŒåŒ…æ‹¬èŒƒå›´å†…ç´¯è®¡çš„tickã€æ¯æµåŠ¨æ€§ç§’æ•°å’ŒtickèŒƒå›´å†…çš„ç§’æ•°
    /// @dev å¿«ç…§åªèƒ½ä¸åœ¨å¤´å¯¸å­˜åœ¨çš„æœŸé—´å†…æ‹æ‘„çš„å…¶ä»–å¿«ç…§è¿›è¡Œæ¯”è¾ƒã€‚
    /// å³ï¼Œå¦‚æœåœ¨æ‹æ‘„ç¬¬ä¸€ä¸ªå¿«ç…§å’Œæ‹æ‘„ç¬¬äºŒä¸ªå¿«ç…§ä¹‹é—´çš„æ•´ä¸ªæœŸé—´å†…æ²¡æœ‰æŒæœ‰å¤´å¯¸ï¼Œåˆ™æ— æ³•æ¯”è¾ƒå¿«ç…§ã€‚
    /// @return tickCumulativeInside èŒƒå›´å†…çš„çš„tickç´¯åŠ å™¨å¿«ç…§
    /// @return secondsPerLiquidityInsideX128 èŒƒå›´å†…çš„æ¯æµåŠ¨æ€§çš„ç§’æ•°
    /// @return secondsInside èŒƒå›´å†…ç»å†çš„ç§’æ•°
    function snapshotCumulativesInside(
        int24 tickLower,
        int24 tickUpper
    )
        external
        view
        override
        noDelegateCall
        returns (int56 tickCumulativeInside, uint160 secondsPerLiquidityInsideX128, uint32 secondsInside)
    {
        checkTicks(tickLower, tickUpper);

        int56 tickCumulativeLower;
        int56 tickCumulativeUpper;
        uint160 secondsPerLiquidityOutsideLowerX128;
        uint160 secondsPerLiquidityOutsideUpperX128;
        uint32 secondsOutsideLower;
        uint32 secondsOutsideUpper;

        // è·å– tickLower å’Œ tickUpper çš„ tick ä¿¡æ¯
        {
            Tick.Info storage lower = ticks[tickLower];
            Tick.Info storage upper = ticks[tickUpper];
            bool initializedLower;
            (tickCumulativeLower, secondsPerLiquidityOutsideLowerX128, secondsOutsideLower, initializedLower) = (
                lower.tickCumulativeOutside,
                lower.secondsPerLiquidityOutsideX128,
                lower.secondsOutside,
                lower.initialized
            );
            require(initializedLower);

            bool initializedUpper;
            (tickCumulativeUpper, secondsPerLiquidityOutsideUpperX128, secondsOutsideUpper, initializedUpper) = (
                upper.tickCumulativeOutside,
                upper.secondsPerLiquidityOutsideX128,
                upper.secondsOutside,
                upper.initialized
            );
            require(initializedUpper);
        }

        Slot0 memory _slot0 = slot0;

        // æ ¹æ®tickä½ç½®ä¸åŒï¼Œå‡æ³•å‚æ•°ä¸åŒï¼Œç†è§£æ¯”è¾ƒç®€å•
        // tickUpper > tickLower > tick
        if (_slot0.tick < tickLower) {
            return (
                tickCumulativeLower - tickCumulativeUpper,
                secondsPerLiquidityOutsideLowerX128 - secondsPerLiquidityOutsideUpperX128,
                secondsOutsideLower - secondsOutsideUpper
            );
        } else if (_slot0.tick < tickUpper) {
            // tickUpper > tick > tickLower
            uint32 time = _blockTimestamp();
            (int56 tickCumulative, uint160 secondsPerLiquidityCumulativeX128) = observations.observeSingle(
                time,
                0,
                _slot0.tick,
                _slot0.observationIndex,
                liquidity,
                _slot0.observationCardinality
            );
            return (
                tickCumulative - tickCumulativeLower - tickCumulativeUpper,
                secondsPerLiquidityCumulativeX128 -
                    secondsPerLiquidityOutsideLowerX128 -
                    secondsPerLiquidityOutsideUpperX128,
                time - secondsOutsideLower - secondsOutsideUpper
            );
        } else {
            // tick > tickUpper > tickLower
            return (
                tickCumulativeUpper - tickCumulativeLower,
                secondsPerLiquidityOutsideUpperX128 - secondsPerLiquidityOutsideLowerX128,
                secondsOutsideUpper - secondsOutsideLower
            );
        }
    }

    /// @inheritdoc IUniswapV3PoolDerivedState
    /// è¯»å–é¢„è¨€æœºï¼Œè·å–å¤šå°‘ç§’ä¹‹å‰ç´¯è®¡çš„tickå’Œæ¯ä¸ªèŒƒå›´å†…æµåŠ¨æ€§å€¼çš„ç´¯è®¡ç§’æ•°
    /// é€šè¿‡ç´¯è®¡tickç®—å‡ºprice
    /// @dev è¦è·å–æ—¶é—´åŠ æƒå¹³å‡åˆ»åº¦æˆ–èŒƒå›´å†…æµåŠ¨æ€§ï¼Œå¿…é¡»ä½¿ç”¨ä¸¤ä¸ªå€¼è°ƒç”¨æ­¤å‡½æ•°ï¼Œä¸€ä¸ªå€¼ä»£è¡¨
    /// æœŸé—´çš„å¼€å§‹ï¼Œå¦ä¸€ä¸ªå€¼ä»£è¡¨æœŸé—´çš„ç»“æŸã€‚ä¾‹å¦‚ï¼Œè¦è·å–æœ€åä¸€å°æ—¶çš„æ—¶é—´åŠ æƒå¹³å‡åˆ»åº¦ï¼Œ
    /// å¿…é¡»ä½¿ç”¨ secondsAgos = [3600, 0] è°ƒç”¨å®ƒã€‚
    function observe(
        uint32[] calldata secondsAgos
    )
        external
        view
        override
        noDelegateCall
        returns (int56[] memory tickCumulatives, uint160[] memory secondsPerLiquidityCumulativeX128s)
    {
        return
            // è¯¦æƒ…æŸ¥çœ‹ Oracle æ–‡ä»¶
            observations.observe(
                _blockTimestamp(),
                secondsAgos,
                slot0.tick,
                slot0.observationIndex,
                liquidity,
                slot0.observationCardinality
            );
    }

    /// @inheritdoc IUniswapV3PoolActions
    /// å¢åŠ è§‚æµ‹æ•°ç»„çš„é•¿åº¦ï¼Œ
    // observationCardinalityNextç”±ç”¨æˆ·ä¼ å…¥ï¼Œå¯ä»¥æ¯”observationCardinalityå¤§å¾ˆå¤š
    function increaseObservationCardinalityNext(
        uint16 observationCardinalityNext
    ) external override lock noDelegateCall {
        uint16 observationCardinalityNextOld = slot0.observationCardinalityNext; // for the event
        uint16 observationCardinalityNextNew = observations.grow(
            observationCardinalityNextOld,
            observationCardinalityNext
        );
        slot0.observationCardinalityNext = observationCardinalityNextNew;
        if (observationCardinalityNextOld != observationCardinalityNextNew)
            emit IncreaseObservationCardinalityNext(observationCardinalityNextOld, observationCardinalityNextNew);
    }

    /// @inheritdoc IUniswapV3PoolActions
    /// @dev not locked because it initializes unlocked
    /// åˆ›å»ºå®Œåˆçº¦ï¼Œéœ€è¦åˆå§‹åŒ–ï¼Œä»»ä½•äººéƒ½å¯ä»¥è°ƒç”¨ï¼Œä½†æ˜¯åªèƒ½è°ƒç”¨ä¸€æ¬¡
    /// åˆå§‹åŒ– sqrtPriceX96, tick, oracle info, feeProtocl, unlocked
    function initialize(uint160 sqrtPriceX96) external override {
        require(slot0.sqrtPriceX96 == 0, 'AI');

        int24 tick = TickMath.getTickAtSqrtRatio(sqrtPriceX96);

        // åˆå§‹åŒ–è§‚æµ‹æ•°ç»„0ç´¢å¼•
        (uint16 cardinality, uint16 cardinalityNext) = observations.initialize(_blockTimestamp());

        slot0 = Slot0({
            sqrtPriceX96: sqrtPriceX96,
            tick: tick,
            observationIndex: 0,
            observationCardinality: cardinality,
            observationCardinalityNext: cardinalityNext,
            feeProtocol: 0,
            unlocked: true
        });

        emit Initialize(sqrtPriceX96, tick);
    }

    struct ModifyPositionParams {
        // the address that owns the position
        address owner;
        // the lower and upper tick of the position
        int24 tickLower;
        int24 tickUpper;
        // any change in liquidity
        // æ”¹å˜çš„liquidityæ•°é‡ï¼Œå¯æ­£å¯è´Ÿ
        int128 liquidityDelta;
    }

    /// @dev Effect some changes to a position
    /// @param params the position details and the change to the position's liquidity to effect
    /// @return position a storage pointer referencing the position with the given owner and tick range
    /// @return amount0 the amount of token0 owed to the pool, negative if the pool should pay the recipient
    /// @return amount1 the amount of token1 owed to the pool, negative if the pool should pay the recipient
    /// ç”¨äº mint/burn æ›´æ–°tickå’Œä»“ä½ä¿¡æ¯
    function _modifyPosition(
        ModifyPositionParams memory params
    ) private noDelegateCall returns (Position.Info storage position, int256 amount0, int256 amount1) {
        checkTicks(params.tickLower, params.tickUpper);

        Slot0 memory _slot0 = slot0; // SLOAD for gas optimization

        // æ›´æ–°tickå’Œä»“ä½ä¿¡æ¯
        position = _updatePosition(
            params.owner,
            params.tickLower,
            params.tickUpper,
            params.liquidityDelta,
            _slot0.tick
        );

        if (params.liquidityDelta != 0) {
            // tickLower > å½“å‰ tickï¼ŒtickåŒºé—´åœ¨å½“å‰tickä¸Šæ–¹ï¼Œåªéœ€è¦æä¾›token0
            // delta x çš„è®¡ç®—å…¬å¼è¯¦è§https://hackmd.io/d4GTJiyrQFigUp80IFb-gQ 6.16
            if (_slot0.tick < params.tickLower) {
                // current tick is below the passed range; liquidity can only become in range by crossing from left to
                // right, when we'll need _more_ token0 (it's becoming more valuable) so user must provide it
                amount0 = SqrtPriceMath.getAmount0Delta(
                    TickMath.getSqrtRatioAtTick(params.tickLower),
                    TickMath.getSqrtRatioAtTick(params.tickUpper),
                    params.liquidityDelta
                );
            } else if (_slot0.tick < params.tickUpper) {
                // current tick is inside the passed range
                // å½“å‰tickåœ¨tickåŒºé—´ä¹‹é—´
                uint128 liquidityBefore = liquidity; // SLOAD for gas optimization

                // write an oracle entry
                // å†™å…¥ä¸€ä¸ªé¢„è¨€æœºçš„è§‚æµ‹æ•°æ®ï¼Œæ›´æ–°å…¨å±€é¢„è¨€æœºä¿¡æ¯ï¼Œè¯¦è§å‡½æ•°æ³¨é‡Š
                (slot0.observationIndex, slot0.observationCardinality) = observations.write(
                    _slot0.observationIndex,
                    _blockTimestamp(),
                    _slot0.tick,
                    liquidityBefore,
                    _slot0.observationCardinality,
                    _slot0.observationCardinalityNext
                );

                // å½“å‰tickåˆ°tickUpperï¼Œè®¡ç®—amount0
                amount0 = SqrtPriceMath.getAmount0Delta(
                    _slot0.sqrtPriceX96,
                    TickMath.getSqrtRatioAtTick(params.tickUpper),
                    params.liquidityDelta
                );
                // tickLoweråˆ°å½“å‰tickï¼Œè®¡ç®—amount1
                amount1 = SqrtPriceMath.getAmount1Delta(
                    TickMath.getSqrtRatioAtTick(params.tickLower),
                    _slot0.sqrtPriceX96,
                    params.liquidityDelta
                );

                // æ›´æ–°å…¨å±€çŠ¶æ€åœ¨èŒƒå›´å†…çš„æµåŠ¨æ€§
                liquidity = LiquidityMath.addDelta(liquidityBefore, params.liquidityDelta);
            } else {
                // current tick is above the passed range; liquidity can only become in range by crossing from right to
                // left, when we'll need _more_ token1 (it's becoming more valuable) so user must provide it
                // tickUpper < å½“å‰ tickï¼ŒtickåŒºé—´åœ¨å½“å‰tickä¸‹æ–¹ï¼Œåªéœ€è¦æä¾›token1
                amount1 = SqrtPriceMath.getAmount1Delta(
                    TickMath.getSqrtRatioAtTick(params.tickLower),
                    TickMath.getSqrtRatioAtTick(params.tickUpper),
                    params.liquidityDelta
                );
            }
        }
    }

    /// @dev Gets and updates a position with the given liquidity delta
    /// @param owner the owner of the position
    /// @param tickLower the lower tick of the position's tick range
    /// @param tickUpper the upper tick of the position's tick range
    /// @param tick the current tick, passed to avoid sloads
    /// æ›´æ–°tickå’Œä»“ä½ä¿¡æ¯
    function _updatePosition(
        address owner,
        int24 tickLower,
        int24 tickUpper,
        int128 liquidityDelta,
        int24 tick
    ) private returns (Position.Info storage position) {
        // è·å–å¯¹åº”çš„ä»“ä½
        position = positions.get(owner, tickLower, tickUpper);

        // æ¯å•ä½æµåŠ¨æ€§è¡¨ç¤ºçš„token0æ‰‹ç»­è´¹
        uint256 _feeGrowthGlobal0X128 = feeGrowthGlobal0X128; // SLOAD for gas optimization
        // æ¯å•ä½æµåŠ¨æ€§è¡¨ç¤ºçš„token1æ‰‹ç»­è´¹
        uint256 _feeGrowthGlobal1X128 = feeGrowthGlobal1X128; // SLOAD for gas optimization

        // if we need to update the ticks, do it
        bool flippedLower;
        bool flippedUpper;
        // å¦‚æœæµåŠ¨æ€§æœ‰å˜åŒ–
        if (liquidityDelta != 0) {
            uint32 time = _blockTimestamp();
            // è·å–å½“å‰çš„ç´¯è®¡ä»·æ ¼å’Œç´¯è®¡æ¯æµåŠ¨æ€§æŒç»­æ—¶é—´
            (int56 tickCumulative, uint160 secondsPerLiquidityCumulativeX128) = observations.observeSingle(
                time,
                0,
                slot0.tick,
                slot0.observationIndex,
                liquidity,
                slot0.observationCardinality
            );

            // æ›´æ–°tickä¿¡æ¯ï¼Œä¸¤ä¸ªfeeå­—æ®µï¼Œä¸¤ä¸ªé¢„è¨€æœºå­—æ®µï¼Œä¸¤ä¸ªæµåŠ¨æ€§å­—æ®µï¼Œè¯¦è§å‡½æ•°æ³¨é‡Š
            flippedLower = ticks.update(
                tickLower,
                tick,
                liquidityDelta,
                _feeGrowthGlobal0X128,
                _feeGrowthGlobal1X128,
                secondsPerLiquidityCumulativeX128,
                tickCumulative,
                time,
                false,
                maxLiquidityPerTick
            );
            flippedUpper = ticks.update(
                tickUpper,
                tick,
                liquidityDelta,
                _feeGrowthGlobal0X128,
                _feeGrowthGlobal1X128,
                secondsPerLiquidityCumulativeX128,
                tickCumulative,
                time,
                true,
                maxLiquidityPerTick
            );

            // ç¿»è½¬tickï¼Œä¼ å…¥tickå¿…é¡»æ˜¯å¯ä»¥æ•´é™¤tické—´è·çš„
            if (flippedLower) {
                tickBitmap.flipTick(tickLower, tickSpacing);
            }
            if (flippedUpper) {
                tickBitmap.flipTick(tickUpper, tickSpacing);
            }
        }

        // è·å–tickèŒƒå›´å†…çš„fee growth
        (uint256 feeGrowthInside0X128, uint256 feeGrowthInside1X128) = ticks.getFeeGrowthInside(
            tickLower,
            tickUpper,
            tick,
            _feeGrowthGlobal0X128,
            _feeGrowthGlobal1X128
        );

        // æ›´æ–°ä»“ä½ä¿¡æ¯ï¼ŒæµåŠ¨æ€§ã€ä¸¤ä¸ªfee growth insdeã€ä¸¤ä¸ªtoken ownedï¼ˆå‡å¦‚æœ‰æ”¶ç›Šï¼‰ï¼Œè¯¦è§å‡½æ•°æ³¨é‡Š
        position.update(liquidityDelta, feeGrowthInside0X128, feeGrowthInside1X128);

        // clear any tick data that is no longer needed
        if (liquidityDelta < 0) {
            // burnæ—¶ï¼Œå¦‚æœéœ€è¦ç¿»è½¬tickï¼Œè¯´æ˜æ²¡æœ‰æµåŠ¨æ€§ï¼Œåˆ é™¤tiks[tick]
            if (flippedLower) {
                ticks.clear(tickLower);
            }
            if (flippedUpper) {
                ticks.clear(tickUpper);
            }
        }
    }

    /// @inheritdoc IUniswapV3PoolActions
    /// @dev noDelegateCall is applied indirectly via _modifyPosition
    // æ·»åŠ æµåŠ¨æ€§
    function mint(
        address recipient,
        int24 tickLower,
        int24 tickUpper,
        // æ·»åŠ çš„æµåŠ¨æ€§æ•°é‡
        uint128 amount,
        bytes calldata data
    ) external override lock returns (uint256 amount0, uint256 amount1) {
        require(amount > 0);
        (, int256 amount0Int, int256 amount1Int) = _modifyPosition(
            ModifyPositionParams({
                owner: recipient,
                tickLower: tickLower,
                tickUpper: tickUpper,
                liquidityDelta: int256(amount).toInt128()
            })
        );

        amount0 = uint256(amount0Int);
        amount1 = uint256(amount1Int);

        uint256 balance0Before;
        uint256 balance1Before;
        if (amount0 > 0) balance0Before = balance0();
        if (amount1 > 0) balance1Before = balance1();
        // è°ƒç”¨åˆçº¦è½¬è´¦
        IUniswapV3MintCallback(msg.sender).uniswapV3MintCallback(amount0, amount1, data);
        if (amount0 > 0) require(balance0Before.add(amount0) <= balance0(), 'M0');
        if (amount1 > 0) require(balance1Before.add(amount1) <= balance1(), 'M1');

        emit Mint(msg.sender, recipient, tickLower, tickUpper, amount, amount0, amount1);
    }

    /// @inheritdoc IUniswapV3PoolActions
    function collect(
        address recipient,
        int24 tickLower,
        int24 tickUpper,
        uint128 amount0Requested,
        uint128 amount1Requested
    ) external override lock returns (uint128 amount0, uint128 amount1) {
        // we don't need to checkTicks here, because invalid positions will never have non-zero tokensOwed{0,1}
        Position.Info storage position = positions.get(msg.sender, tickLower, tickUpper);

        amount0 = amount0Requested > position.tokensOwed0 ? position.tokensOwed0 : amount0Requested;
        amount1 = amount1Requested > position.tokensOwed1 ? position.tokensOwed1 : amount1Requested;

        if (amount0 > 0) {
            position.tokensOwed0 -= amount0;
            TransferHelper.safeTransfer(token0, recipient, amount0);
        }
        if (amount1 > 0) {
            position.tokensOwed1 -= amount1;
            TransferHelper.safeTransfer(token1, recipient, amount1);
        }

        emit Collect(msg.sender, recipient, tickLower, tickUpper, amount0, amount1);
    }

    /// @inheritdoc IUniswapV3PoolActions
    /// @dev noDelegateCall is applied indirectly via _modifyPosition
    // ç§»é™¤æµåŠ¨æ€§
    function burn(
        int24 tickLower,
        int24 tickUpper,
        uint128 amount
    ) external override lock returns (uint256 amount0, uint256 amount1) {
        // æ›´æ–°tickå’Œä»“ä½ä¿¡æ¯
        (Position.Info storage position, int256 amount0Int, int256 amount1Int) = _modifyPosition(
            ModifyPositionParams({
                owner: msg.sender,
                tickLower: tickLower,
                tickUpper: tickUpper,
                liquidityDelta: -int256(amount).toInt128()
            })
        );

        amount0 = uint256(-amount0Int);
        amount1 = uint256(-amount1Int);

        if (amount0 > 0 || amount1 > 0) {
            // æŠŠéœ€è¦é€€è¿˜çš„tokenåŠ åˆ°ownedä¸Šï¼Œé€šè¿‡collecté¢†å–
            (position.tokensOwed0, position.tokensOwed1) = (
                position.tokensOwed0 + uint128(amount0),
                position.tokensOwed1 + uint128(amount1)
            );
        }

        emit Burn(msg.sender, tickLower, tickUpper, amount, amount0, amount1);
    }

    struct SwapCache {
        // the protocol fee for the input token
        uint8 feeProtocol;
        // liquidity at the beginning of the swap
        // ç¼“å­˜åˆå§‹æµåŠ¨æ€§
        uint128 liquidityStart;
        // the timestamp of the current block
        uint32 blockTimestamp;
        // the current value of the tick accumulator, computed only if we cross an initialized tick
        int56 tickCumulative;
        // the current value of seconds per liquidity accumulator, computed only if we cross an initialized tick
        uint160 secondsPerLiquidityCumulativeX128;
        // whether we've computed and cached the above two accumulators
        bool computedLatestObservation;
    }

    // the top level state of the swap, the results of which are recorded in storage at the end
    struct SwapState {
        // the amount remaining to be swapped in/out of the input/output asset
        int256 amountSpecifiedRemaining;
        // the amount already swapped out/in of the output/input asset
        int256 amountCalculated;
        // current sqrt(price)
        uint160 sqrtPriceX96;
        // the tick associated with the current price
        int24 tick;
        // the global fee growth of the input token
        // è¾“å…¥tokençš„å…¨å±€feeå¢é‡
        uint256 feeGrowthGlobalX128;
        // amount of input token paid as protocol fee
        // è¾“å…¥tokenæ”¯ä»˜çš„åè®®è´¹
        uint128 protocolFee;
        // the current liquidity in range
        uint128 liquidity;
    }

    struct StepComputations {
        // the price at the beginning of the step
        uint160 sqrtPriceStartX96;
        // the next tick to swap to from the current tick in the swap direction
        int24 tickNext;
        // whether tickNext is initialized or not
        bool initialized;
        // sqrt(price) for the next tick (1/0)
        uint160 sqrtPriceNextX96;
        // how much is being swapped in in this step
        uint256 amountIn;
        // how much is being swapped out
        uint256 amountOut;
        // how much fee is being paid in
        uint256 feeAmount;
    }

    /// @inheritdoc IUniswapV3PoolActions
    // äº¤æ¢token
    // æ ¹æ®zeroForOneå’ŒexactInputï¼Œå¯ä»¥æœ‰å››ç§swapç»„åˆï¼š
    // 1. true true	è¾“å…¥å›ºå®šæ•°é‡token0ï¼Œè¾“å‡ºæœ€å¤§æ•°é‡token1
    // 2. true false è¾“å…¥æœ€å°æ•°é‡token0ï¼Œè¾“å‡ºå›ºå®šæ•°é‡token1
    // 3. false	true è¾“å…¥å›ºå®šæ•°é‡token1ï¼Œè¾“å‡ºæœ€å¤§æ•°é‡token0
    // 4. false	false è¾“å…¥æœ€å°æ•°é‡token1ï¼Œè¾“å‡ºå›ºå®šæ•°é‡token0
    function swap(
        address recipient,
        bool zeroForOne,
        // æŒ‡å®šçš„ä»£å¸æ•°é‡ï¼Œå¦‚æœä¸ºæ­£ï¼Œè¡¨ç¤ºå¸Œæœ›è¾“å…¥çš„ä»£å¸æ•°é‡ï¼›å¦‚æœä¸ºè´Ÿï¼Œåˆ™è¡¨ç¤ºå¸Œæœ›è¾“å‡ºçš„ä»£å¸æ•°é‡
        int256 amountSpecified,
        // èƒ½å¤Ÿæ‰¿å—çš„ä»·æ ¼ä¸Šé™ï¼ˆæˆ–ä¸‹é™ï¼‰ï¼Œæ ¼å¼ä¸ºQ64.96ï¼›
        // å¦‚æœä»token0åˆ°token1ï¼Œåˆ™è¡¨ç¤ºswapè¿‡ç¨‹ä¸­çš„ä»·æ ¼ä¸‹é™ï¼›
        // å¦‚æœä»token1åˆ°token0ï¼Œåˆ™è¡¨ç¤ºä»·æ ¼ä¸Šé™ï¼›å¦‚æœä»·æ ¼è¶…è¿‡è¯¥å€¼ï¼Œåˆ™swapå¤±è´¥
        uint160 sqrtPriceLimitX96,
        bytes calldata data
    ) external override noDelegateCall returns (int256 amount0, int256 amount1) {
        require(amountSpecified != 0, 'AS');

        Slot0 memory slot0Start = slot0;

        require(slot0Start.unlocked, 'LOK');
        // å¦‚æœzeroForOneï¼Œä»·æ ¼ä¼šå˜å°ï¼Œé‚£ä¹ˆä»·æ ¼é™åˆ¶å¿…é¡»å°äºå½“å‰ä»·æ ¼ï¼Œè€Œä¸”å¤§äºæœ€å°ä»·æ ¼
        // å¦åˆ™ï¼Œä»·æ ¼ä¼šå˜å¤§ï¼Œä»·æ ¼é™åˆ¶å¿…é¡»å¤§äºå½“å‰ä»·æ ¼ï¼Œè€Œä¸”å°äºæœ€å¤§ä»·æ ¼
        require(
            zeroForOne
                ? sqrtPriceLimitX96 < slot0Start.sqrtPriceX96 && sqrtPriceLimitX96 > TickMath.MIN_SQRT_RATIO
                : sqrtPriceLimitX96 > slot0Start.sqrtPriceX96 && sqrtPriceLimitX96 < TickMath.MAX_SQRT_RATIO,
            'SPL'
        );

        slot0.unlocked = false;

        // 1. åˆå§‹åŒ–swapçŠ¶æ€
        // swapç¼“å­˜
        SwapCache memory cache = SwapCache({
            liquidityStart: liquidity,
            blockTimestamp: _blockTimestamp(),
            // 0æ¢1ä½¿ç”¨token0æ‰‹ç»­è´¹
            feeProtocol: zeroForOne ? (slot0Start.feeProtocol % 16) : (slot0Start.feeProtocol >> 4),
            secondsPerLiquidityCumulativeX128: 0,
            tickCumulative: 0,
            computedLatestObservation: false
        });

        // æ˜¯å¦æ˜¯ç²¾ç¡®è¾“å…¥
        bool exactInput = amountSpecified > 0;

        // swapçŠ¶æ€
        SwapState memory state = SwapState({
            // ç²¾ç¡®è¾“å…¥æˆ–è¾“å‡ºçš„ä»£å¸å‰©ä½™çš„æ•°é‡
            amountSpecifiedRemaining: amountSpecified,
            amountCalculated: 0,
            sqrtPriceX96: slot0Start.sqrtPriceX96,
            tick: slot0Start.tick,
            // 0æ¢1æ›´æ–°token0çš„å…¨å±€fee
            feeGrowthGlobalX128: zeroForOne ? feeGrowthGlobal0X128 : feeGrowthGlobal1X128,
            protocolFee: 0,
            liquidity: cache.liquidityStart
        });

        // 2. å¾ªç¯äº¤æ¢ï¼Œç›´åˆ°å‰©ä½™æ•°é‡ç­‰äº0æˆ–ä»·æ ¼ç­‰äºé™åˆ¶ä»·æ ¼
        // continue swapping as long as we haven't used the entire input/output and haven't reached the price limit
        // åªè¦æˆ‘ä»¬æ²¡æœ‰ç”¨å®Œæ‰€æœ‰è¾“å…¥/è¾“å‡ºï¼Œå¹¶ä¸”æ²¡æœ‰è¾¾åˆ°ä»·æ ¼é™åˆ¶ï¼Œå°±ç»§ç»­äº¤æ¢
        while (state.amountSpecifiedRemaining != 0 && state.sqrtPriceX96 != sqrtPriceLimitX96) {
            // 2.1 åˆ†æ­¥äº¤æ¢
            StepComputations memory step;

            // æ¯ä¸€æ­¥çš„åˆå§‹ä»·æ ¼ç­‰äºä¸Šä¸€ä¸ªstepä¹‹åçš„ä»·æ ¼
            step.sqrtPriceStartX96 = state.sqrtPriceX96;

            // æ ¹æ®å½“å‰tickï¼Œå¯»æ‰¾æœ€è¿‘çš„å·²åˆå§‹åŒ–çš„tickï¼Œæˆ–è€…æœ¬ç»„ç¬¬ä¸€ä¸ªtick
            (step.tickNext, step.initialized) = tickBitmap.nextInitializedTickWithinOneWord(
                state.tick,
                tickSpacing,
                zeroForOne
            );

            // ensure that we do not overshoot the min/max tick, as the tick bitmap is not aware of these bounds
            // ç¡®ä¿tickä¸è¶…å‡ºç•Œé™
            if (step.tickNext < TickMath.MIN_TICK) {
                step.tickNext = TickMath.MIN_TICK;
            } else if (step.tickNext > TickMath.MAX_TICK) {
                step.tickNext = TickMath.MAX_TICK;
            }

            // get the price for the next tick
            // è®¡ç®—tickNextå¯¹åº”çš„ä»·æ ¼
            step.sqrtPriceNextX96 = TickMath.getSqrtRatioAtTick(step.tickNext);

            // compute values to swap to the target tick, price limit, or point where input/output amount is exhausted
            // å®Œæˆä¸€æ­¥äº¤æ¢
            // äº¤æ¢åçš„ä»·æ ¼, æ¶ˆè€—çš„è¾“å…¥ä»£å¸æ•°é‡, å¾—åˆ°çš„è¾“å‡ºä»£å¸æ•°é‡, è¿™ä¸€æ­¥çš„æ‰‹ç»­è´¹æ•°é‡ = å®Œæˆä¸€æ­¥äº¤æ¢(åˆå§‹ä»·æ ¼, ç›®æ ‡ä»·æ ¼, å¯ç”¨æµåŠ¨æ€§, å‰©ä½™ä»£å¸ï¼Œæ‰‹ç»­è´¹)
            (state.sqrtPriceX96, step.amountIn, step.amountOut, step.feeAmount) = SwapMath.computeSwapStep(
                state.sqrtPriceX96,
                // 0æ¢1ï¼šsqrtPriceLimitX96ä¸ºä»·æ ¼ä¸‹é™ï¼Œä½¿ç”¨min(sqrtPriceNextX96, sqrtPriceLimitX96)
                // 1æ¢0ï¼šsqrtPriceLimitX96ä¸ºä»·æ ¼ä¸Šé™ï¼Œä½¿ç”¨max(sqrtPriceNextX96, sqrtPriceLimitX96)
                // ä¿è¯ç›®æ ‡ä»·æ ¼ä¸èƒ½è¶…å‡ºé™åˆ¶
                (zeroForOne ? step.sqrtPriceNextX96 < sqrtPriceLimitX96 : step.sqrtPriceNextX96 > sqrtPriceLimitX96)
                    ? sqrtPriceLimitX96
                    : step.sqrtPriceNextX96,
                state.liquidity,
                state.amountSpecifiedRemaining,
                fee
            );

            // æ›´æ–°å‰©ä½™ä»£å¸å’Œå·²å…‘æ¢ä»£å¸æ•°é‡
            // æ³¨æ„ï¼šè¿™é‡ŒamountSpecifiedRemainingå’ŒamountInå¯èƒ½è¡¨ç¤ºtoken0ï¼Œä¹Ÿå¯èƒ½è¡¨ç¤ºtoken1ï¼Œå…·ä½“å–å†³äºzeroForOneçš„å€¼ï¼Œä½†æ˜¯å®ƒä¿©æ˜¯åŒ¹é…çš„ï¼Œè¦ä¹ˆéƒ½ä¸ºtoken0ï¼Œè¦ä¹ˆéƒ½ä¸ºtoken1ï¼Œç»“åˆ4ä¸­swapç±»å‹ï¼Œå°±å®¹æ˜“ç†è§£äº†ã€‚ä¸‹é¢å¾ªç¯swapå®Œæˆä¹‹åï¼Œä¼šæ ¹æ®zeroForOneå’ŒexactInputå€¼ï¼Œå¾—åˆ°æ­£ç¡®å¯¹åº”çš„amount0å’Œamount1
            if (exactInput) {
                // å¦‚æœæ˜¯ç²¾ç¡®è¾“å…¥ï¼ŒamountSpecifiedRemainingä¸ºæ­£å€¼ï¼Œéœ€è¦å‡å»amountInå’ŒfeeAmountï¼Œå‡å°‘å‰©ä½™ä»£å¸æ•°é‡
                state.amountSpecifiedRemaining -= (step.amountIn + step.feeAmount).toInt256();
                // amountCalculatedé»˜è®¤ä¸º0ï¼Œå‡å»æ­£å€¼ï¼Œå¾—åˆ°è´Ÿæ•°ç›¸åŠ ï¼Œå¢åŠ å·²å…‘æ¢ä»£å¸æ•°é‡
                // è¿™é‡ŒamountCalculatedè¡¨ç¤ºè¾“å‡ºtokenæ•°é‡
                state.amountCalculated = state.amountCalculated.sub(step.amountOut.toInt256());
            } else {
                // å¦‚æœæ˜¯ç²¾ç¡®è¾“å‡ºï¼ŒamountSpecifiedRemainingä¸ºè´Ÿå€¼ï¼Œéœ€è¦åŠ ä¸ŠamountOutï¼Œå‡å°‘å‰©ä½™ä»£å¸æ•°é‡
                state.amountSpecifiedRemaining += step.amountOut.toInt256();
                // amountCalculated åŠ ä¸Šæ­£å€¼ï¼Œå¢åŠ è¾“å…¥ä»£å¸æ•°é‡
                // è¿™é‡ŒamountCalculatedè¡¨ç¤ºè¾“å…¥tokenæ•°é‡
                state.amountCalculated = state.amountCalculated.add((step.amountIn + step.feeAmount).toInt256());
            }

            // if the protocol fee is on, calculate how much is owed, decrement feeAmount, and increment protocolFee
            // æ›´æ–°åè®®æ‰‹ç»­è´¹
            if (cache.feeProtocol > 0) {
                uint256 delta = step.feeAmount / cache.feeProtocol;
                step.feeAmount -= delta;
                state.protocolFee += uint128(delta);
            }

            // update global fee tracker
            // æ›´æ–°å…¨å±€feeæ•°é‡ (feeAmount / liquidity)
            if (state.liquidity > 0)
                state.feeGrowthGlobalX128 += FullMath.mulDiv(step.feeAmount, FixedPoint128.Q128, state.liquidity);

            // shift tick if we reached the next price
            // å¦‚æœäº¤æ¢åä»·æ ¼ç­‰äºä¸‹ä¸€ä¸ªtickçš„ä»·æ ¼ï¼Œè¯´æ˜è·¨äº†tickï¼Œéœ€è¦æ›´æ–°tick outsideä¿¡æ¯ï¼Œæ›´æ–°èŒƒå›´å†…æµåŠ¨æ€§å’Œtick
            if (state.sqrtPriceX96 == step.sqrtPriceNextX96) {
                // if the tick is initialized, run the tick transition
                // å¦‚æœtickå·²ç»åˆå§‹åŒ–ï¼Œè·¨tickæ›´æ–°tickä¿¡æ¯ã€æ›´æ–°èŒƒå›´å†…æµåŠ¨æ€§
                if (step.initialized) {
                    // check for the placeholder value, which we replace with the actual value the first time the swap
                    // crosses an initialized tick
                    // å¾ªç¯ä¸­åªéœ€è¦è·å–ä¸€æ¬¡è§‚æµ‹æ•°æ®
                    if (!cache.computedLatestObservation) {
                        // è·å–é¢„è¨€æœºç´¯è®¡tickï¼Œç´¯è®¡æ¯æµåŠ¨æ€§æŒç»­æ—¶é•¿
                        (cache.tickCumulative, cache.secondsPerLiquidityCumulativeX128) = observations.observeSingle(
                            cache.blockTimestamp,
                            0,
                            slot0Start.tick,
                            slot0Start.observationIndex,
                            cache.liquidityStart,
                            slot0Start.observationCardinality
                        );
                        cache.computedLatestObservation = true;
                    }
                    // è°ƒç”¨crossï¼Œæ›´æ–°tick outsideä¿¡æ¯ï¼Œè·å–å‡€æµåŠ¨æ€§
                    int128 liquidityNet = ticks.cross(
                        step.tickNext,
                        (zeroForOne ? state.feeGrowthGlobalX128 : feeGrowthGlobal0X128),
                        (zeroForOne ? feeGrowthGlobal1X128 : state.feeGrowthGlobalX128),
                        cache.secondsPerLiquidityCumulativeX128,
                        cache.tickCumulative,
                        cache.blockTimestamp
                    );
                    // if we're moving leftward, we interpret liquidityNet as the opposite sign
                    // safe because liquidityNet cannot be type(int128).min
                    // tickçš„æµåŠ¨æ€§å­˜åœ¨ä¸‹ç•Œï¼Œç©¿è¿‡ä¸‹ç•Œå¢åŠ ï¼Œç©¿è¿‡ä¸Šç•Œå‡å°‘

                    // å½“zeroForOneä¸ºtrueï¼Œä»·æ ¼å‘ä¸‹ç§»åŠ¨
                    // äº¤æ˜“ä¼šç©¿è¶Šä»·æ ¼åŒºé—´çš„ä¸‹ç•Œ tick å’Œä¸Šç•Œ tick:

                    // ç©¿è¶Šä¸‹ç•Œ tick (lower):
                    // å½“ä»·æ ¼å‘ä¸‹ç©¿è¶Šä¸‹ç•Œ tick æ—¶,æ„å‘³ç€æµåŠ¨æ€§åŒºé—´çš„ä¸‹è¾¹ç•Œè¢«äº¤æ˜“"æ¶ˆè€—"äº†ã€‚
                    // å› æ­¤,åœ¨ä¸‹ç•Œ tick å¤„,æµåŠ¨æ€§åº”è¯¥å‡å°‘ã€‚

                    // ç©¿è¶Šä¸Šç•Œ tick (upper):
                    // å½“ä»·æ ¼å‘ä¸‹ç©¿è¶Šä¸Šç•Œ tick æ—¶,æ„å‘³ç€äº¤æ˜“è¿›å…¥äº†ä¸€ä¸ªæ–°çš„æµåŠ¨æ€§åŒºé—´ã€‚
                    // å› æ­¤,åœ¨ä¸Šç•Œ tick å¤„,æ–°çš„æµåŠ¨æ€§è¢«"æ¿€æ´»",æµåŠ¨æ€§åº”è¯¥å¢åŠ ã€‚
                    // æ‰€ä»¥ zeroForOne ä¸º true æ—¶, å–å liquidityDelta:

                    // å–å liquidityDelta ,ç¡®ä¿äº†å½“ä»·æ ¼ä¸‹é™æ—¶:
                    // ä¸‹ç•Œ tick çš„æµåŠ¨æ€§å‡å°‘ (liquidityDelta å˜ä¸ºè´Ÿæ•°)
                    // ä¸Šç•Œ tick çš„æµåŠ¨æ€§å¢åŠ  (liquidityDelta å˜ä¸ºæ­£æ•°)
                    if (zeroForOne) liquidityNet = -liquidityNet;
                    // æ›´æ–°èŒƒå›´å†…æµåŠ¨æ€§
                    state.liquidity = LiquidityMath.addDelta(state.liquidity, liquidityNet);
                }

                // ç§»åŠ¨å½“å‰tickåˆ°ä¸‹ä¸€ä¸ªtick
                state.tick = zeroForOne ? step.tickNext - 1 : step.tickNext;
            } else if (state.sqrtPriceX96 != step.sqrtPriceStartX96) {
                // recompute unless we're on a lower tick boundary (i.e. already transitioned ticks), and haven't moved
                // æ²¡è·¨tickï¼Œä½†æ˜¯ä¹Ÿéœ€è¦ä½¿ç”¨äº¤æ¢åçš„ä»·æ ¼è®¡ç®—æœ€æ–°çš„tickå€¼
                state.tick = TickMath.getTickAtSqrtRatio(state.sqrtPriceX96);
            }
        }

        // 3. æ›´æ–°å…¨å±€ä»·æ ¼ï¼Œtickå’Œé¢„è¨€æœºä¿¡æ¯
        // update tick and write an oracle entry if the tick change
        // å¦‚æœäº¤æ¢åçš„tickä¸äº¤æ¢å‰çš„tickä¸åŒ
        if (state.tick != slot0Start.tick) {
            // è®°å½•ä¸€æ¬¡ï¼ˆé¢„è¨€æœºï¼‰è§‚æµ‹ç‚¹æ•°æ®ï¼Œå› ä¸ºè·¨äº†tick
            (uint16 observationIndex, uint16 observationCardinality) = observations.write(
                slot0Start.observationIndex,
                cache.blockTimestamp,
                slot0Start.tick,
                cache.liquidityStart,
                slot0Start.observationCardinality,
                slot0Start.observationCardinalityNext
            );
            // æ›´æ–°å…¨å±€çŠ¶æ€ï¼šä»·æ ¼ã€tickã€é¢„è¨€æœºä¿¡æ¯
            // æ³¨æ„æ­¤æ—¶sqrtPriceX96ä¸tickå¹¶ä¸ä¸€å®šå¯¹åº”ï¼ŒsqrtPriceX96æ‰èƒ½å‡†ç¡®åæ˜ å½“å‰ä»·æ ¼
            (slot0.sqrtPriceX96, slot0.tick, slot0.observationIndex, slot0.observationCardinality) = (
                state.sqrtPriceX96,
                state.tick,
                observationIndex,
                observationCardinality
            );
        } else {
            // otherwise just update the price
            // å¦‚æœäº¤æ¢å‰åtickå€¼ç›¸åŒï¼Œåˆ™åªéœ€è¦ä¿®æ”¹ä»·æ ¼
            slot0.sqrtPriceX96 = state.sqrtPriceX96;
        }

        // 4. æ›´æ–°æµåŠ¨æ€§
        // update liquidity if it changed
        // å¦‚æœå…¨å±€æµåŠ¨æ€§å‘ç”Ÿæ”¹å˜ï¼Œåˆ™æ›´æ–°liquidity
        if (cache.liquidityStart != state.liquidity) liquidity = state.liquidity;

        // update fee growth global and, if necessary, protocol fees
        // overflow is acceptable, protocol has to withdraw before it hits type(uint128).max fees
        // 4. æ›´æ–°ç´¯è®¡æ‰‹ç»­è´¹å’Œåè®®æ‰‹ç»­è´¹
        if (zeroForOne) {
            // 0æ¢1ï¼Œæ”¶å–token0ä½œä¸ºæ‰‹ç»­è´¹ï¼Œæ›´æ–°feeGrowthGlobal 0
            feeGrowthGlobal0X128 = state.feeGrowthGlobalX128;
            if (state.protocolFee > 0) protocolFees.token0 += state.protocolFee;
        } else {
            // 1æ¢0ï¼Œæ”¶å–token1ä½œä¸ºæ‰‹ç»­è´¹ï¼Œæ›´æ–°feeGrowthGlobal 1
            feeGrowthGlobal1X128 = state.feeGrowthGlobalX128;
            if (state.protocolFee > 0) protocolFees.token1 += state.protocolFee;
        }

        // 5. è®¡ç®—æœ¬æ¬¡äº¤æ¢éœ€è¦çš„amount0å’Œamount1ï¼Œè®©amountå’ŒtokenåŒ¹é…
        // æ ¹æ®zeroForOneå’ŒexactInputï¼Œå¯ä»¥æœ‰å››ç§swapç»„åˆï¼š
        // 1. true true	è¾“å…¥å›ºå®šæ•°é‡token0ï¼Œè¾“å‡ºæœ€å¤§æ•°é‡token1
        // 2. true false è¾“å…¥æœ€å°æ•°é‡token0ï¼Œè¾“å‡ºå›ºå®šæ•°é‡token1
        // 3. false	true è¾“å…¥å›ºå®šæ•°é‡token1ï¼Œè¾“å‡ºæœ€å¤§æ•°é‡token0
        // 4. false	false è¾“å…¥æœ€å°æ•°é‡token1ï¼Œè¾“å‡ºå›ºå®šæ•°é‡token0

        // éƒ½ä¸ºtrueè¡¨ç¤ºï¼Œ0æ¢1ä¸”ç²¾ç¡®è¾“å…¥
        // éƒ½ä¸ºfalseè¡¨ç¤ºï¼Œ1æ¢0ä¸”ç²¾ç¡®è¾“å‡º
        // è¿™æ—¶å€™ï¼ŒamountSpecified è¡¨ç¤ºçš„éƒ½æ˜¯token0ï¼Œæ‰€ä»¥ç”¨amountSpecified - state.amountSpecifiedRemainingè¡¨ç¤ºamount0
        // state.amountCalculatedè¡¨ç¤ºçš„éƒ½æ˜¯token1ï¼Œæ‰€ä»¥ç”¨å®ƒè¡¨ç¤ºamount1
        // ä¸ç›¸ç­‰ï¼Œä¸€ä¸ªtrue ä¸€ä¸ªfalse
        // è¿™æ—¶å€™ï¼ŒamountSpecified è¡¨ç¤ºçš„éƒ½æ˜¯token1ï¼Œå’Œä¸Šé¢æ­£å¥½ç›¸å
        (amount0, amount1) = zeroForOne == exactInput
            ? (amountSpecified - state.amountSpecifiedRemaining, state.amountCalculated)
            : (state.amountCalculated, amountSpecified - state.amountSpecifiedRemaining);

        // do the transfers and collect payment
        // 6. è½¬ç§»token
        if (zeroForOne) {
            // è¾“å‡ºtokenè½¬ç»™recipient
            if (amount1 < 0) TransferHelper.safeTransfer(token1, recipient, uint256(-amount1));

            uint256 balance0Before = balance0();
            // è¾“å…¥tokené€šè¿‡callbackè½¬åˆ°pool
            IUniswapV3SwapCallback(msg.sender).uniswapV3SwapCallback(amount0, amount1, data);
            require(balance0Before.add(uint256(amount0)) <= balance0(), 'IIA');
        } else {
            // è¾“å‡ºtokenè½¬ç»™recipient
            if (amount0 < 0) TransferHelper.safeTransfer(token0, recipient, uint256(-amount0));

            uint256 balance1Before = balance1();
            // è¾“å…¥tokené€šè¿‡callbackè½¬åˆ°pool
            IUniswapV3SwapCallback(msg.sender).uniswapV3SwapCallback(amount0, amount1, data);
            require(balance1Before.add(uint256(amount1)) <= balance1(), 'IIA');
        }

        emit Swap(msg.sender, recipient, amount0, amount1, state.sqrtPriceX96, state.liquidity, state.tick);
        // è§£é™¤é‡å…¥é”
        slot0.unlocked = true;
    }

    /// @inheritdoc IUniswapV3PoolActions
    // é—ªç”µè´·
    function flash(
        address recipient,
        uint256 amount0,
        uint256 amount1,
        bytes calldata data
    ) external override lock noDelegateCall {
        uint128 _liquidity = liquidity;
        require(_liquidity > 0, 'L');

        // è®¡ç®—æ‰‹ç»­è´¹
        uint256 fee0 = FullMath.mulDivRoundingUp(amount0, fee, 1e6);
        uint256 fee1 = FullMath.mulDivRoundingUp(amount1, fee, 1e6);
        uint256 balance0Before = balance0();
        uint256 balance1Before = balance1();

        // è½¬ç§»tokenç»™recipient
        if (amount0 > 0) TransferHelper.safeTransfer(token0, recipient, amount0);
        if (amount1 > 0) TransferHelper.safeTransfer(token1, recipient, amount1);

        // è°ƒç”¨è°ƒç”¨è€…çš„callbackï¼Œéœ€è¦æŠŠtokenè¿˜å›æ¥
        IUniswapV3FlashCallback(msg.sender).uniswapV3FlashCallback(fee0, fee1, data);

        uint256 balance0After = balance0();
        uint256 balance1After = balance1();

        require(balance0Before.add(fee0) <= balance0After, 'F0');
        require(balance1Before.add(fee1) <= balance1After, 'F1');

        // sub is safe because we know balanceAfter is gt balanceBefore by at least fee
        // æ± å­å®é™…å¾—åˆ°çš„æ‰‹ç»­è´¹
        uint256 paid0 = balance0After - balance0Before;
        uint256 paid1 = balance1After - balance1Before;

        if (paid0 > 0) {
            uint8 feeProtocol0 = slot0.feeProtocol % 16;
            uint256 fees0 = feeProtocol0 == 0 ? 0 : paid0 / feeProtocol0;
            if (uint128(fees0) > 0) protocolFees.token0 += uint128(fees0);
            // ç´¯è®¡å…¨å±€æ‰‹ç»­è´¹
            feeGrowthGlobal0X128 += FullMath.mulDiv(paid0 - fees0, FixedPoint128.Q128, _liquidity);
        }
        if (paid1 > 0) {
            uint8 feeProtocol1 = slot0.feeProtocol >> 4;
            uint256 fees1 = feeProtocol1 == 0 ? 0 : paid1 / feeProtocol1;
            if (uint128(fees1) > 0) protocolFees.token1 += uint128(fees1);
            // ç´¯è®¡å…¨å±€æ‰‹ç»­è´¹
            feeGrowthGlobal1X128 += FullMath.mulDiv(paid1 - fees1, FixedPoint128.Q128, _liquidity);
        }

        emit Flash(msg.sender, recipient, amount0, amount1, paid0, paid1);
    }

    /// @inheritdoc IUniswapV3PoolOwnerActions
    function setFeeProtocol(uint8 feeProtocol0, uint8 feeProtocol1) external override lock onlyFactoryOwner {
        require(
            (feeProtocol0 == 0 || (feeProtocol0 >= 4 && feeProtocol0 <= 10)) &&
                (feeProtocol1 == 0 || (feeProtocol1 >= 4 && feeProtocol1 <= 10))
        );
        uint8 feeProtocolOld = slot0.feeProtocol;
        slot0.feeProtocol = feeProtocol0 + (feeProtocol1 << 4);
        emit SetFeeProtocol(feeProtocolOld % 16, feeProtocolOld >> 4, feeProtocol0, feeProtocol1);
    }

    /// @inheritdoc IUniswapV3PoolOwnerActions
    function collectProtocol(
        address recipient,
        uint128 amount0Requested,
        uint128 amount1Requested
    ) external override lock onlyFactoryOwner returns (uint128 amount0, uint128 amount1) {
        amount0 = amount0Requested > protocolFees.token0 ? protocolFees.token0 : amount0Requested;
        amount1 = amount1Requested > protocolFees.token1 ? protocolFees.token1 : amount1Requested;

        if (amount0 > 0) {
            if (amount0 == protocolFees.token0) amount0--; // ensure that the slot is not cleared, for gas savings
            protocolFees.token0 -= amount0;
            TransferHelper.safeTransfer(token0, recipient, amount0);
        }
        if (amount1 > 0) {
            if (amount1 == protocolFees.token1) amount1--; // ensure that the slot is not cleared, for gas savings
            protocolFees.token1 -= amount1;
            TransferHelper.safeTransfer(token1, recipient, amount1);
        }

        emit CollectProtocol(msg.sender, recipient, amount0, amount1);
    }
}
