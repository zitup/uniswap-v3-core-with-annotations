// SPDX-License-Identifier: BUSL-1.1
pragma solidity >=0.5.0 <0.8.0;

import './FullMath.sol';
import './FixedPoint128.sol';
import './LiquidityMath.sol';

/// @title Position
/// @notice Positions represent an owner address' liquidity between a lower and upper tick boundary
/// @dev Positions store additional state for tracking fees owed to the position
library Position {
    // info stored for each user's position
    struct Info {
        // the amount of liquidity owned by this position
        // è¡¨ç¤ºä¸Šä¸€æ¬¡å¤´å¯¸æ›´æ–°æ—¶ï¼Œè¯¥å¤´å¯¸æ‰€è¡¨ç¤ºçš„è™šæ‹ŸæµåŠ¨æ€§æ•°é‡ã€‚
        // = sqrt(x * y) ð‘¥å’Œð‘¦åˆ†åˆ«è¡¨ç¤ºåœ¨ä»»æ„æ—¶åˆ»è¯¥å¤´å¯¸è¿›å…¥ä»·æ ¼åŒºé—´æ—¶ï¼Œè™šæ‹Ÿtoken0å’Œtoken1çš„æ•°é‡
        // ä¸ŽUniswap v2ä¸åŒï¼ˆæ¯ä¸ªæµåŠ¨æ€§ä»½é¢éšæ—¶é—´å¢žé•¿ï¼‰ï¼Œv3çš„æµåŠ¨æ€§ä»½é¢å¹¶ä¸æ”¹å˜ï¼Œå› ä¸ºæ‰‹ç»­è´¹æ˜¯å•ç‹¬ç´¯è®¡ã€‚å®ƒæ€»æ˜¯ç­‰ä»·äºŽ sqrt(x * y)
        uint128 liquidity;
        // fee growth per unit of liquidity as of the last update to liquidity or fees owed
        // ä¸Šä¸€æ¬¡æ›´æ–°çš„æ¯å•ä½æµåŠ¨æ€§ä»£è¡¨çš„token0æ‰‹ç»­è´¹ï¼Œä¹Ÿå°±æ˜¯ä¸Šæ¬¡é¢†å–æ‰‹ç»­è´¹æ—¶çš„æ¯å•ä½æµåŠ¨æ€§è¡¨ç¤ºçš„token0æ‰‹ç»­è´¹
        // å†æ¬¡é¢†å–æ—¶ï¼Œéœ€è¦å‡åŽ»è¿™ä¸ªå€¼ï¼Œåªé¢†å–æ‰‹ç»­è´¹çš„å¢žé‡
        uint256 feeGrowthInside0LastX128;
        // ä¸Šä¸€æ¬¡æ›´æ–°çš„æ¯å•ä½æµåŠ¨æ€§ä»£è¡¨çš„token1æ‰‹ç»­è´¹ï¼Œä¹Ÿå°±æ˜¯ä¸Šæ¬¡é¢†å–æ‰‹ç»­è´¹æ—¶çš„æ¯å•ä½æµåŠ¨æ€§è¡¨ç¤ºçš„token1æ‰‹ç»­è´¹
        // å†æ¬¡é¢†å–æ—¶ï¼Œéœ€è¦å‡åŽ»è¿™ä¸ªå€¼ï¼Œåªé¢†å–æ‰‹ç»­è´¹çš„å¢žé‡
        uint256 feeGrowthInside1LastX128;
        // the fees owed to the position owner in token0/token1
        // token0 è¡¨ç¤ºçš„æœªé¢†å–æ‰‹ç»­è´¹
        uint128 tokensOwed0;
        // token1 è¡¨ç¤ºçš„æœªé¢†å–æ‰‹ç»­è´¹
        uint128 tokensOwed1;
    }

    /// @notice Returns the Info struct of a position, given an owner and position boundaries
    /// @param self The mapping containing all user positions
    /// @param owner The address of the position owner
    /// @param tickLower The lower tick boundary of the position
    /// @param tickUpper The upper tick boundary of the position
    /// @return position The position info struct of the given owners' position
    function get(
        mapping(bytes32 => Info) storage self,
        address owner,
        int24 tickLower,
        int24 tickUpper
    ) internal view returns (Position.Info storage position) {
        position = self[keccak256(abi.encodePacked(owner, tickLower, tickUpper))];
    }

    /// @notice Credits accumulated fees to a user's position
    /// @param self The individual position to update
    /// @param liquidityDelta The change in pool liquidity as a result of the position update
    /// @param feeGrowthInside0X128 The all-time fee growth in token0, per unit of liquidity, inside the position's tick boundaries
    /// @param feeGrowthInside1X128 The all-time fee growth in token1, per unit of liquidity, inside the position's tick boundaries
    // æ›´æ–°å¤´å¯¸çš„æµåŠ¨æ€§å’Œå¯å–å›žä»£å¸ï¼Œåªåœ¨mint/burnæ—¶æ›´æ–°
    function update(
        Info storage self,
        int128 liquidityDelta,
        uint256 feeGrowthInside0X128,
        uint256 feeGrowthInside1X128
    ) internal {
        Info memory _self = self;

        uint128 liquidityNext;
        if (liquidityDelta == 0) {
            require(_self.liquidity > 0, 'NP'); // disallow pokes for 0 liquidity positions
            liquidityNext = _self.liquidity;
        } else {
            liquidityNext = LiquidityMath.addDelta(_self.liquidity, liquidityDelta);
        }

        // calculate accumulated fees
        // è®¡ç®—ç´¯è®¡çš„æ‰‹ç»­è´¹
        // ç¬¬ä¸€æ¬¡mintæ—¶ï¼Œ_self.liquidityä¸º0ï¼Œæ‰€ä»¥tokensOwed0å’ŒtokensOwed1éƒ½ä¸º0
        // åŒä¸€ä¸ªtickèŒƒå›´ï¼Œæ–°å¢žæµåŠ¨æ€§æ—¶ï¼Œå‡åŽ»ä¸Šä¸€æ¬¡å·²ç»è®¡ç®—è¿‡çš„feeGrowthInside0LastX128ï¼Œä¹˜ä»¥ä¹‹å‰çš„æµåŠ¨æ€§ï¼Œè¡¨ç¤ºå¢žåŠ çš„fee
        uint128 tokensOwed0 = uint128(
            FullMath.mulDiv(feeGrowthInside0X128 - _self.feeGrowthInside0LastX128, _self.liquidity, FixedPoint128.Q128)
        );
        uint128 tokensOwed1 = uint128(
            FullMath.mulDiv(feeGrowthInside1X128 - _self.feeGrowthInside1LastX128, _self.liquidity, FixedPoint128.Q128)
        );

        // update the position
        // æ›´æ–°ä»“ä½æœ€æ–°æµåŠ¨æ€§å’Œè®¡ç®—è¿‡çš„fee growth inside
        if (liquidityDelta != 0) self.liquidity = liquidityNext;
        self.feeGrowthInside0LastX128 = feeGrowthInside0X128;
        self.feeGrowthInside1LastX128 = feeGrowthInside1X128;
        // å¦‚æžœæœ‰æ”¶ç›Šï¼Œæ›´æ–°æ”¶ç›Š
        if (tokensOwed0 > 0 || tokensOwed1 > 0) {
            // overflow is acceptable, have to withdraw before you hit type(uint128).max fees
            self.tokensOwed0 += tokensOwed0;
            self.tokensOwed1 += tokensOwed1;
        }
    }
}
