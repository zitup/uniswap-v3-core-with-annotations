// SPDX-License-Identifier: BUSL-1.1
pragma solidity >=0.5.0 <0.8.0;

import './LowGasSafeMath.sol';
import './SafeCast.sol';

import './TickMath.sol';
import './LiquidityMath.sol';

/// @title Tick
/// @notice Contains functions for managing tick processes and relevant calculations
library Tick {
    using LowGasSafeMath for int256;
    using SafeCast for int256;

    // info stored for each initialized individual tick
    struct Info {
        // the total position liquidity that references this tick
        // å½“å‰ tick çš„æ€»ä»“ä½æµåŠ¨æ€§æ€»å’Œï¼Œmintå’ŒburnæµåŠ¨æ€§æ—¶ä¼šæ›´æ–°ï¼Œåˆ†åˆ«å¢åŠ å’Œå‡å°‘
        // åˆ¤æ–­tickæ˜¯å¦æœ‰æµåŠ¨æ€§å…³è”
        uint128 liquidityGross;
        // amount of net liquidity added (subtracted) when tick is crossed from left to right (right to left),
        // å½“å‰ tick çš„å‡€æµåŠ¨æ€§ï¼Œè€ƒè™‘æ­£è´Ÿï¼Œmintå’Œburnæ—¶ä¼šæ›´æ–°ï¼Œè¡¨ç¤ºå½“ç©¿è¿‡è¿™ä¸ªtickæ—¶ï¼Œåº”è¯¥å¢åŠ æˆ–å‡å°‘çš„æµåŠ¨æ€§
        // mintå’Œburnæ€»å…±å¯¹åº”å››ç§æƒ…å†µï¼š
        // 1. mint - uppper tick, å‡å°‘æµåŠ¨æ€§
        // 2. mint - lower tick, æ·»åŠ æµåŠ¨æ€§
        // ä¹Ÿå°±æ˜¯è¯´ï¼Œmintæ—¶åœ¨è¿™ä¸ªåŒºé—´æ·»åŠ çš„æµåŠ¨æ€§ï¼Œæ˜¯æ·»åŠ åœ¨äº†lower tickä¸Šï¼Œå‡å°‘åœ¨upper tick
        // 3. burn - upper tick, æ·»åŠ æµåŠ¨æ€§
        // 4. burn - lower tick, å‡å°‘æµåŠ¨æ€§
        // burnå’Œmintç›¸åï¼Œéœ€è¦ç§»é™¤æµåŠ¨æ€§ï¼Œå‡å°‘åœ¨loweræ·»åŠ çš„æµåŠ¨æ€§ï¼Œå¢åŠ åœ¨upperå‡å°‘çš„æµåŠ¨æ€§
        int128 liquidityNet;
        // fee growth per unit of liquidity on the _other_ side of this tick (relative to the current tick)
        // only has relative meaning, not absolute â€” the value depends on when the tick is initialized
        uint256 feeGrowthOutside0X128;
        uint256 feeGrowthOutside1X128;
        // ä¸‹é¢ä¸‰ä¸ªå˜é‡ä¸ä¼šè¢«åˆçº¦å†…éƒ¨ä½¿ç”¨ï¼Œè€Œæ˜¯å¸®åŠ©å¤–éƒ¨åˆçº¦ï¼ˆå¦‚åŸºäºv3çš„æµåŠ¨æ€§æŒ–çŸ¿åˆçº¦ï¼‰æ›´æ–¹ä¾¿åœ°è·å–åˆçº¦ä¿¡æ¯ã€‚
        // the cumulative tick value on the other side of the tick
        // è·Ÿè¸ª oracle çš„ tickCumulative
        // æ­¤tickå¦ä¸€ä¾§çš„ tickCumulative
        int56 tickCumulativeOutside;
        // the seconds per unit of liquidity on the _other_ side of this tick (relative to the current tick)
        // only has relative meaning, not absolute â€” the value depends on when the tick is initialized
        // è·Ÿè¸ª oracle çš„ secondsPerLiquidityCumulative
        // æ­¤tickå¦ä¸€ä¾§ï¼ˆç›¸å¯¹äºå½“å‰tickï¼‰æ¯å•ä½æµåŠ¨æ€§çš„ç§’æ•°ã€‚ä»…å…·æœ‰ç›¸å¯¹æ„ä¹‰ï¼Œè€Œéç»å¯¹æ„ä¹‰â€”â€”è¯¥å€¼å–å†³äºtickçš„åˆå§‹åŒ–æ—¶é—´
        uint160 secondsPerLiquidityOutsideX128;
        // the seconds spent on the other side of the tick (relative to the current tick)
        // only has relative meaning, not absolute â€” the value depends on when the tick is initialized
        // tick å¦ä¸€ä¾§ï¼ˆç›¸å¯¹äºå½“å‰tickï¼‰èŠ±è´¹çš„ç§’æ•°
        // ä»…å…·æœ‰ç›¸å¯¹æ„ä¹‰ï¼Œè€Œéç»å¯¹æ„ä¹‰â€”â€”è¯¥å€¼å–å†³äºtickçš„åˆå§‹åŒ–æ—¶é—´
        uint32 secondsOutside;
        // true if the tick is initialized, i.e. the value is exactly equivalent to the expression liquidityGross != 0
        // these 8 bits are set to prevent fresh sstores when crossing newly initialized ticks
        // liquidationGross != 0 åˆ™ä¸º trueï¼Œè¡¨ç¤ºå·²ç»åˆå§‹åŒ–
        bool initialized;
    }

    /// @notice Derives max liquidity per tick from given tick spacing
    /// @dev Executed within the pool constructor
    /// @param tickSpacing The amount of required tick separation, realized in multiples of `tickSpacing`
    ///     e.g., a tickSpacing of 3 requires ticks to be initialized every 3rd tick i.e., ..., -6, -3, 0, 3, 6, ...
    /// @return The max liquidity per tick
    function tickSpacingToMaxLiquidityPerTick(int24 tickSpacing) internal pure returns (uint128) {
        int24 minTick = (TickMath.MIN_TICK / tickSpacing) * tickSpacing;
        int24 maxTick = (TickMath.MAX_TICK / tickSpacing) * tickSpacing;
        uint24 numTicks = uint24((maxTick - minTick) / tickSpacing) + 1;
        return type(uint128).max / numTicks;
    }

    /// @notice Retrieves fee growth data
    /// @param self The mapping containing all tick information for initialized ticks
    /// @param tickLower The lower tick boundary of the position
    /// @param tickUpper The upper tick boundary of the position
    /// @param tickCurrent The current tick
    /// @param feeGrowthGlobal0X128 The all-time global fee growth, per unit of liquidity, in token0
    /// @param feeGrowthGlobal1X128 The all-time global fee growth, per unit of liquidity, in token1
    /// @return feeGrowthInside0X128 The all-time fee growth in token0, per unit of liquidity, inside the position's tick boundaries
    /// @return feeGrowthInside1X128 The all-time fee growth in token1, per unit of liquidity, inside the position's tick boundaries
    // è®¡ç®—ä¸¤ä¸ªtickåŒºé—´å†…éƒ¨çš„æ¯æµåŠ¨æ€§ç´¯ç§¯æ‰‹ç»­è´¹
    function getFeeGrowthInside(
        mapping(int24 => Tick.Info) storage self,
        int24 tickLower,
        int24 tickUpper,
        int24 tickCurrent,
        uint256 feeGrowthGlobal0X128,
        uint256 feeGrowthGlobal1X128
    ) internal view returns (uint256 feeGrowthInside0X128, uint256 feeGrowthInside1X128) {
        Info storage lower = self[tickLower];
        Info storage upper = self[tickUpper];

        // calculate fee growth below
        // 1. è®¡ç®—tickLowerçš„ä¸‹æ–¹outside fee
        uint256 feeGrowthBelow0X128;
        uint256 feeGrowthBelow1X128;
        if (tickCurrent >= tickLower) {
            // å¦‚æœå½“å‰tickå¤§äºtickLowerï¼Œé‚£ä¹ˆç›´æ¥ä½¿ç”¨tickLowerçš„outside fee
            feeGrowthBelow0X128 = lower.feeGrowthOutside0X128;
            feeGrowthBelow1X128 = lower.feeGrowthOutside1X128;
        } else {
            // å¦‚æœå½“å‰tickå°äºtickLowerï¼Œæ­¤æ—¶tickLowerçš„outsideæ˜¯ä¸Šæ–¹fee
            // éœ€è¦ç”¨globalå‡å»tickLowerçš„outsideå¾—åˆ°tickLowerä¸‹æ–¹çš„outside fee
            feeGrowthBelow0X128 = feeGrowthGlobal0X128 - lower.feeGrowthOutside0X128;
            feeGrowthBelow1X128 = feeGrowthGlobal1X128 - lower.feeGrowthOutside1X128;
        }

        // calculate fee growth above
        // 2. è®¡ç®—tickUpperçš„outside fee
        uint256 feeGrowthAbove0X128;
        uint256 feeGrowthAbove1X128;
        if (tickCurrent < tickUpper) {
            // å¦‚æœå½“å‰tickå°äºtickUpperï¼Œé‚£ä¹ˆç›´æ¥ä½¿ç”¨tickUpperçš„outside fee
            feeGrowthAbove0X128 = upper.feeGrowthOutside0X128;
            feeGrowthAbove1X128 = upper.feeGrowthOutside1X128;
        } else {
            // å¦‚æœå½“å‰tickå¤§äºç­‰äºtickUpperï¼Œæ­¤æ—¶tickUpperçš„outsideæ˜¯ä¸‹æ–¹fee
            // éœ€è¦ç”¨globalå‡å»tickUpperçš„outsideå¾—åˆ°tickUpperä¸Šæ–¹çš„outside fee
            feeGrowthAbove0X128 = feeGrowthGlobal0X128 - upper.feeGrowthOutside0X128;
            feeGrowthAbove1X128 = feeGrowthGlobal1X128 - upper.feeGrowthOutside1X128;
        }

        // 3. fee inside = global - lower outside - uppper outside;
        feeGrowthInside0X128 = feeGrowthGlobal0X128 - feeGrowthBelow0X128 - feeGrowthAbove0X128;
        feeGrowthInside1X128 = feeGrowthGlobal1X128 - feeGrowthBelow1X128 - feeGrowthAbove1X128;
    }

    /// @notice Updates a tick and returns true if the tick was flipped from initialized to uninitialized, or vice versa
    /// @param self The mapping containing all tick information for initialized ticks
    /// @param tick The tick that will be updated
    /// @param tickCurrent The current tick
    /// @param liquidityDelta A new amount of liquidity to be added (subtracted) when tick is crossed from left to right (right to left)
    /// @param feeGrowthGlobal0X128 The all-time global fee growth, per unit of liquidity, in token0
    /// @param feeGrowthGlobal1X128 The all-time global fee growth, per unit of liquidity, in token1
    /// @param secondsPerLiquidityCumulativeX128 The all-time seconds per max(1, liquidity) of the pool
    /// @param tickCumulative The tick * time elapsed since the pool was first initialized
    /// @param time The current block timestamp cast to a uint32
    /// @param upper true for updating a position's upper tick, or false for updating a position's lower tick
    /// @param maxLiquidity The maximum liquidity allocation for a single tick
    /// @return flipped Whether the tick was flipped from initialized to uninitialized, or vice versa
    // mint/burn æ›´æ–°tickä¿¡æ¯
    function update(
        mapping(int24 => Tick.Info) storage self,
        int24 tick,
        int24 tickCurrent,
        int128 liquidityDelta,
        uint256 feeGrowthGlobal0X128,
        uint256 feeGrowthGlobal1X128,
        uint160 secondsPerLiquidityCumulativeX128,
        int56 tickCumulative,
        uint32 time,
        bool upper,
        uint128 maxLiquidity
    ) internal returns (bool flipped) {
        Tick.Info storage info = self[tick];

        uint128 liquidityGrossBefore = info.liquidityGross;
        // å¢åŠ è¿™ä¸ªtickçš„æ€»æµåŠ¨æ€§
        uint128 liquidityGrossAfter = LiquidityMath.addDelta(liquidityGrossBefore, liquidityDelta);

        // æ€»æµåŠ¨æ€§å¿…é¡»å°äºmaxLiquidity
        require(liquidityGrossAfter <= maxLiquidity, 'LO');

        // ä¸€ä¸ªä¸ºtrueï¼Œä¸€ä¸ªä¸ºfalseæ—¶ï¼Œéœ€è¦flip tick
        flipped = (liquidityGrossAfter == 0) != (liquidityGrossBefore == 0);

        // åªæœ‰è¢«è‡³å°‘ä¸€ä¸ªå¤´å¯¸ä½œä¸ºè¾¹ç•Œç«¯ç‚¹çš„tickæ‰éœ€è¦ğ‘“ğ‘œ(fee outside)ã€‚
        // å› æ­¤ï¼Œå‡ºäºæ•ˆç‡è€ƒè™‘ï¼Œğ‘“ğ‘œä¸ä¼šè¢«åˆå§‹åŒ–ï¼ˆä¹Ÿå› æ­¤ï¼Œå½“tickè¢«ç©¿è¶Šæ—¶æ— éœ€è¢«æ›´æ–°ï¼‰ï¼Œç›´åˆ°å½“ä½¿ç”¨è¯¥tickä½œä¸ºè¾¹ç•Œç‚¹åˆ›å»ºå¤´å¯¸æ—¶(liquidityGrossBefore == 0)æ‰ä¼šåˆå§‹åŒ–ã€‚
        if (liquidityGrossBefore == 0) {
            // by convention, we assume that all growth before a tick was initialized happened _below_ the tick
            // å½“tick ğ‘–çš„ğ‘“ğ‘œåˆå§‹åŒ–æ—¶ï¼Œå®ƒçš„åˆå§‹å€¼è¢«è®¾ç½®æˆå½“å‰æ‰€æœ‰çš„æ‰‹ç»­è´¹éƒ½ç”±å°äºè¯¥tickæ—¶æ”¶å–
            if (tick <= tickCurrent) {
                // fee growth
                info.feeGrowthOutside0X128 = feeGrowthGlobal0X128;
                info.feeGrowthOutside1X128 = feeGrowthGlobal1X128;
                // oracle ç›¸å…³çš„æ¯æµåŠ¨æ€§æŒç»­æ—¶é—´ï¼Œç´¯è®¡tick
                info.secondsPerLiquidityOutsideX128 = secondsPerLiquidityCumulativeX128;
                info.tickCumulativeOutside = tickCumulative;
                info.secondsOutside = time;
            }
            info.initialized = true;
        }

        // æ›´æ–°ä¸¤ä¸ªæµåŠ¨æ€§å­—æ®µ
        info.liquidityGross = liquidityGrossAfter;

        // when the lower (upper) tick is crossed left to right (right to left), liquidity must be added (removed)
        // liquidityNet çš„å€¼å¯ä»¥æ˜¯æ­£æ•°(è¡¨ç¤ºå‡€æµåŠ¨æ€§å¢åŠ ),ä¹Ÿå¯ä»¥æ˜¯è´Ÿæ•°(è¡¨ç¤ºå‡€æµåŠ¨æ€§å‡å°‘)ï¼Œå¯¹åº”å››ç§æƒ…å†µ
        // å½“æµåŠ¨æ€§æä¾›è€…æ·»åŠ æµåŠ¨æ€§æ—¶:
        // å¯¹äºä»·æ ¼åŒºé—´çš„ä¸‹ç•Œ tick (lower),liquidityNet å¢åŠ  liquidityDelta,å› ä¸ºè¿™ä¸ª tick æ˜¯æµåŠ¨æ€§çš„èµ·ç‚¹ã€‚
        // å¯¹äºä»·æ ¼åŒºé—´çš„ä¸Šç•Œ tick (upper),liquidityNet å‡å°‘ liquidityDelta,å› ä¸ºè¿™ä¸ª tick æ˜¯æµåŠ¨æ€§çš„ç»ˆç‚¹ã€‚

        // å½“æµåŠ¨æ€§æä¾›è€…ç§»é™¤æµåŠ¨æ€§æ—¶,æƒ…å†µæ­£å¥½ç›¸å:
        // å¯¹äºä»·æ ¼åŒºé—´çš„ä¸‹ç•Œ tick (lower),liquidityNet å‡å°‘ liquidityDelta,å› ä¸ºä»è¿™ä¸ª tick å¼€å§‹ç§»é™¤æµåŠ¨æ€§ã€‚
        // å¯¹äºä»·æ ¼åŒºé—´çš„ä¸Šç•Œ tick (upper),liquidityNet å¢åŠ  liquidityDelta,å› ä¸ºåœ¨è¿™ä¸ª tick å¤„æµåŠ¨æ€§çš„ç§»é™¤ç»“æŸã€‚
        // è¿™ç§å¤„ç†æ–¹å¼ç¡®ä¿äº†æ¯ä¸ª tick çš„ liquidityNet æ­£ç¡®åæ˜ äº†æµåŠ¨æ€§çš„å‡€å˜åŒ–ã€‚å¯¹äºä»·æ ¼åŒºé—´å†…çš„æ‰€æœ‰å…¶ä»– tick,å®ƒä»¬çš„ liquidityNet ä¿æŒä¸å˜,å› ä¸ºè¿™äº› tick éƒ½è¢«å®Œæ•´åœ°è¦†ç›–åœ¨æµåŠ¨æ€§åŒºé—´å†…ã€‚
        info.liquidityNet = upper
            ? int256(info.liquidityNet).sub(liquidityDelta).toInt128()
            : int256(info.liquidityNet).add(liquidityDelta).toInt128();
    }

    /// @notice Clears tick data
    /// @param self The mapping containing all initialized tick information for initialized ticks
    /// @param tick The tick that will be cleared
    function clear(mapping(int24 => Tick.Info) storage self, int24 tick) internal {
        delete self[tick];
    }

    /// @notice Transitions to next tick as needed by price movement
    /// @param self The mapping containing all tick information for initialized ticks
    /// @param tick The destination tick of the transition
    /// @param feeGrowthGlobal0X128 The all-time global fee growth, per unit of liquidity, in token0
    /// @param feeGrowthGlobal1X128 The all-time global fee growth, per unit of liquidity, in token1
    /// @param secondsPerLiquidityCumulativeX128 The current seconds per liquidity
    /// @param tickCumulative The tick * time elapsed since the pool was first initialized
    /// @param time The current block.timestamp
    /// @return liquidityNet The amount of liquidity added (subtracted) when tick is crossed from left to right (right to left)
    // è·¨tickæ—¶ï¼Œæ›´æ–°tickä¿¡æ¯
    function cross(
        mapping(int24 => Tick.Info) storage self,
        int24 tick,
        uint256 feeGrowthGlobal0X128,
        uint256 feeGrowthGlobal1X128,
        uint160 secondsPerLiquidityCumulativeX128,
        int56 tickCumulative,
        uint32 time
    ) internal returns (int128 liquidityNet) {
        Tick.Info storage info = self[tick];
        // å…¨å±€å‡å»ä¸Šæ¬¡çš„outside = å½“å‰å¦ä¸€ä¾§çš„ outside
        info.feeGrowthOutside0X128 = feeGrowthGlobal0X128 - info.feeGrowthOutside0X128;
        info.feeGrowthOutside1X128 = feeGrowthGlobal1X128 - info.feeGrowthOutside1X128;
        info.secondsPerLiquidityOutsideX128 = secondsPerLiquidityCumulativeX128 - info.secondsPerLiquidityOutsideX128;
        info.tickCumulativeOutside = tickCumulative - info.tickCumulativeOutside;
        info.secondsOutside = time - info.secondsOutside;
        // è¿”å›å‡€æµåŠ¨æ€§
        liquidityNet = info.liquidityNet;
    }
}
